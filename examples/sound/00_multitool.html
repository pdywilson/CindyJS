<!DOCTYPE html>
<html>
<head>
    <meta charset="UTF-8">

    <title>Multi Music Tool</title>
    <style type="text/css">
        * {
            margin: 0px;
            padding: 0px;
            background-color: #222222;

        }

        #CSConsole {
            background-color: #FAFAFA;
            border-top: 1px solid #333333;
            bottom: 0px;
            height: 200px;
            overflow-y: scroll;
            position: fixed;
            width: 100%;
        }
    </style>
<script type="text/javascript" src="../../build/js/Cindy.js"></script>
  <!--script type="text/javascript" src="http://science-to-touch.com/Music/sound/apps/common/js/Cindy.js"></script-->

<script id="csinit" type="text/x-cindyscript">
//....TOP LEVEL UI


fingerstokeys=apply(1..20,-99);
fingerstofreqs=apply(1..20,-99);

timbreselected=false;
tuningselected=false;
analyseselected=false;
timbrebuttonpos=(-15.5,9);
tuningbuttonpos=(-12.5,9);
analysebuttonpos=(-9.5,9);
mainiconextend=2.5;

timbrebox=[
  [-15.5,1],
  [-8,1],
  [-8,8.5],
  [-15.5,8.5]
];

tuningbox=[
  [-15.5,1],
  [-7,1],
  [-7,8.5],
  [-15.5,8.5]
];

analyserbox=[
  [-15.5,1],
  [-6,1],
  [-6,8.5],
  [-15.5,8.5]
];


//....BUTTONS

buttons=[
  [//TIMBRE
   [(-15,7),"sineTimbreIcon_"],
   [(-15,5.7),"pulseTimbreIcon_"],
   [(-15,4.4),"sawTimbreIcon_"],
   [(-15,3.1),"squareTimbreIcon_"],
   [(-15,1.8),"triangleTimbreIcon_"],
   [(-11,7),"pickTimbreIcon_"],
   [(-11,5.5),"dampTimbreIcon_"],
   [(-11,4),"holdTimbreIcon_"]
  ],
  [//TUNING
   [(-15,7),"gleichstufig"],
   [(-15,6),"pythagoräisch"],
   [(-15,5),"rein"],
   [(-15,4),"indisch"]
  ],
  [//ANALYSE
   [(-15,5.5),"dissonanceIcon_dissonance"],
   [(-12,5.5),"ratiosIcon_ratios"],
   [(-15,2),"lissajousIcon_lissajous"],
   [(-9,5.5),"fifthIcon_circle 5th"],
   [(-12,2),"waveformIcon_wave"]
  ]
];

timbre1=[1,0,0,0,0,0,0,0];
phases1=[0,0,0,0,0,0,0,0];
timbre2=[1,1,1,1,1,1,1,1];
phases2=[1,1,1,1,1,1,1,1]*pi/2;
timbre3=[1,1/2,1/3,1/4,1/5,1/6,1/7,1/8];
phases3=[0,0,0,0,0,0,0,0];
timbre4=[1,0,1/3,0,1/5,0,1/7,0];
phases4=[0,0,0,0,0,0,0,0];
timbre5=[1,0,1/9,0,1/25,0,1/49,0];
phases5=[0,0,1,0,0,0,1,0]*pi;


apply(1..length(buttons),i,
  apply(1..length(buttons_i),j,b=buttons_i_j;
    tok=tokenize(buttons_i_j_2,"_");
    if(length(tok)==2,
      asp=(1,1);//TODO QUICKHACK WORKAROUND
      if(i==1,asp=(2,1));
      box=[b_1,b_1+(2.5,0),b_1+(2.5,2.5*asp_2/asp_1),b_1+(0,2.5*asp_2/asp_1)];
    ,
      box=[b_1,b_1+(3,0),b_1+(3,.8),b_1+(0,0.8)];
    );
    buttons_i_j=buttons_i_j++[box,false];
  );
);

buttons_1_3_4=true;
buttons_2_1_4=true;
buttons_3_1_4=true;
buttons_1_7_4=true;

analyser="dissonance";
//....ANALYSER
playanimation();
setanalyser(a):=(
//   stopanimation();
   analyser=a;
   if(a=="wave",playanimation();timestamp=seconds();oldsh=0;);
   if(a=="lissajous",playanimation();timestamp=seconds();ta=0;tb=0;tadamp=0;tbdamp=0;);

);


//....TIMBRE


settimbre(timbre3);
setphases(phases3);

setphases(p):=(phases=p);

settimbre(t):=(
timbre=t;
apply(1..8,
  (reg_#).y=(base_#).y+timbre_#*((top_#).y-(base_#).y);
  );
);


setdampinghold():=(tonedamping=.0001;toneduration=100;tonehold=true);
setdampingdamp():=(tonedamping=3;toneduration=3;tonehold=false);
setdampingpick():=(tonedamping=20;toneduration=1;tonehold=false);
setdampingdamp();


//....TUNING

tuning=apply(0..11,(2^(1/12))^#);

tuningequal=apply(0..11,(2^(1/12))^#);



tuningjust=(1,16/15,9/8,6/5,5/4,4/3,45/32,3/2,8/5,5/3,16/9,15/8);

tuningpyth=(1,(2/3)^5*8,(3/2)^2/2,(2/3)^3*4,(3/2)^4/4,4/3,(3/2)^6/8,3/2,(2/3)^4*8,(3/2)^3/2,(2/3)^2*4,(3/2)^5/4);

//     1 2       3     4    5   6     7   8   9     10  11    12    13    14  15     16  17  18    19   20  21   22
//     S,r1,     r2,   R1,  R2, g1,   g2, G1, G2,   M1, M2,   m1,   m2,   P,  d1,    d2, D1, D2,   n1,  n2, N1,  N2,     S
indie=(1,256/243,16/15,10/9,9/8,32/27,6/5,5/4,81/64,4/3,27/20,45/32,40/27,3/2,128/81,8/5,5/3,27/16,16/9,9/5,15/8,243/128,2);

shrutis="S,r1,r2,R1,R2,g1,g2,G1,G2,M1,M2,m1,m2,P,d1,d2,D1,D2,n1,n2,N1,N2,S'";

//7.    Basant Bukhari (P:S) _ S, r1, G1, M1, P, d1, n1

indie7=(1,1,256/243,256/243,5/4,4/3,4/3,3/2,3/2,128/81,128/81,16/9);

settuning(t):=(
  tuning=t;


);

tuning=tuningequal;
//tuning=indie7;


tunenote(note):=(
     octave=floor(note/12+.001);
     nkey=note-octave*12;
     ((tuning_(round(nkey+1)))*2^octave)^ssca;
);



//....MIDI
beating=false;
linesh=0;
hitkeys=[];
linesoftones=zerovector(120);
keys=["A","W","S","E","D","F","T","G","Z","H","U","J","K","O","L","P","º","Þ","Ü"];
callmidi(m,a,b):=(
 if(m==144,hitkeys=set(hitkeys++[a-60]));
 if(m==128,hitkeys=set(hitkeys--[a-60]));
 mdia=a;
 mdib=b;
 mdim=m;
 playa=a;
 playb=b;
 if(m==176,
    re=a-13;
    timbre_re=b/127;
   (reg_re).y=(base_re).y+b/127*((top_re).y-(base_re).y);


 );
 if(m==144,
 tadamp=0;
 tbdamp=0;

 if(analyser=="lissajous",
 clearimage("osci1");
 clearimage("osci0");
 adv=0;
 advlen=0;
 oldpt=(0,0);
);
   play=tunenote(a-60)*f1;
   partials=apply(1..8,(#^sp)/#);
   if(!beating,


     playsin(play,duration->toneduration, damp->tonedamping,line->linesh,harmonics->harm,partials->partials,amp->1);
     linesoftones_a=[linesh];
     linesh=mod(linesh+1,8);
  ,
     playsin(play+beats*5,duration->toneduration, damp->tonedamping,line->linesh,harmonics->harm,partials->partials,amp->1);
     playsin(play-beats*5,duration->toneduration, damp->tonedamping,line->linesh+1,harmonics->harm,partials->partials,amp->1);
     linesoftones_a=[linesh,linesh+1];
     linesh=mod(linesh+2,16);
   );
 );
 if(m==128,

  if(tonehold,
    apply(linesoftones_a,l,
       playsin(0,damp->10000,amp->1,harmonics->[0,0,0,0,0,0,0,0],line->l));
    );
    linesoftones_a=0;
 );
);



stopfromline(ind):=(
       playsin(0,amp->1,harmonics->[0,0,0,0,0,0,0,0],line->ind*2+20);
);

playfromline(x,ind):=(

   play=((2^(1/12))^x)*f1;
   partials=apply(1..8,(#^sp)/#);
   println("play: "+x+" "+play+" "+ind+" "+partials);
   if(!beating,


     playsin(play,duration->100,line->ind*2+20,harmonics->harm,partials->partials,amp->1);

  ,
     playsin(play+beats*5,duration->100, damp->0,line->ind*2,harmonics->harm,partials->partials,amp->1);
     playsin(play-beats*5,duration->100, damp->0,line->ind*2+1,harmonics->harm,partials->partials,amp->1);

   );
);

//....UI


PL.x=5;
PR.x=5+7;
SL.x=5;
SR.x=5+7;

base=[C,E,G,K,M,O,Q,S];
top=[B,D,F,H,L,N,P,R];
reg=[T,U,V,W,X,Y,Z,P0];
apply(reg,#.size=7);
//apply(top,#.y=10);
//apply(base,#.y=7);
settimbre(timbre3);
setphases(phases3);


f1=300;

harm=(1,0,1/2,0,1/3,0,1/4);
harm=(1,1,1,1,1);
A.y=0;




//....DISSONANCE CURVE

s(f1,f2):=0.24/(0.021*f1+19);;
dis1(f1,f2):=(
ss=s(f1,f2);
exp(-3.5*ss*(f2-f1))-exp(-5.75*ss*(f2-f1))
);

dis(f1,f2):=if(f1<f2,dis1(f1,f2),dis1(f2,f1));



sdis(f1,f2):=(
all=
  apply(1..length(harm),(f1*(#^sp),harm_#))++
  apply(1..length(harm),(f2*(#^sp),harm_#));
all=select(all,#_2>.001);



pairs=pairs(all);


sum(pairs,int,
   (int_1_2*int_2_2)*dis(int_1_1,int_2_1)
);
);

//Das ist von hand zurechtgefummelt.
movorig(x):=if(ftype==0,(x+12)/36*26.8-13.6,
if(ftype==1,(2^(x/12))*5.95-10.6,-(2^(-x/12))*8.94+4.27));
;

invmov(x):=((x+13.6)/26.8*36-12);
resample():=(

n=1000;
if(moving,n=200);
sample=apply(0..n,
x=#/n*3*12-12;
f2=f1*2^(x/12);
(movorig(x),40*sdis(f1,f2))
);
    resampled=true;

);
sp=1;
resample();
    resampled=false;

moving=false;


//....BASIC ROUTINES

ftype=0;// 0--> LOG(F)   1-->F    2-->  -1/F

mov(x):=(x+12)/36*26.8-13.6;

dtune(note):=(
  fre=tunenote(note);
  if(ftype==0,log(fre)/log(2)*12,
     if(ftype==1,fre*8-8,-1/fre*12+12));
);
mov(x):=(dtune(x)+12)/36*26.8-13.6;

//....HELPERS


cf(x,n):=(

  if(n==0,["*"],
   if(round(x*1000)==round(x)*1000,
     [x],
     [floor(x)]++cf(1/(x-floor(x)),n-1)
   );
 );
);

bruch1(cf):=(

   if(length(cf)==1,
    [cf_1,1]
   ,
    zw=bruch1(cf_(2..length(cf)));
    [zw_1*cf_1+zw_2,zw_1];
   )
);


bruch(x,cf):=
if(cf_(-1)=="*",
  "$"+format(x,2)+"$",
  erg=bruch1(cf);
  "$"+erg_1+"\over"+erg_2+"$";
);

myguess(x):=(
   cf=cf(x,10);
   bruch(x,cf);
);

trysqrts(x):=(
  bas=2^(1/12);
  regional(test);
  test=log(x)/log(bas);
  if(test~=round(test),
     ggt=ggt(test,12);
     test=test/ggt;
     root=12/ggt;
     if(root~=2,root="",root="["+12/ggt+"]");
     if(test~=1,
       "$\sqrt"+root+"{2}$",
       "$(\sqrt"+root+"{2})^{"+test+"}$"
       )
     ,
     "*"
  );

);


myguess(a,b):=(
   cf1=cf(a,10);
   cf2=cf(b,10);
   if(cf1_(-1)!="*" & cf2_(-1)!="*",
     erg2=bruch1(cf2);
     erg1=bruch1(cf1);
     fakt1=(erg2_1)*(erg1_2);
     fakt2=(erg2_2)*(erg1_1);
     ggt=ggt(round(fakt1),round(fakt2));

      "$"+fakt2/ggt+"\over"+fakt1/ggt+"$";
    ,
      erg=trysqrts(a/b);
      if(erg=="*",
        "$"+format(a/b,2)+"$",
        erg;
      );
    );
);

ggt1(a,b):=(
   if(b==0,a,ggt1(b,mod(a,b)));
);
ggt(a,b):=(
  if(a>b,ggt1(a,b),ggt1(b,a));
);


//....LISSAJOUS
createimage("osci0", 1000, 1000);
createimage("osci1", 1000, 1000);
currentosci = 0; //pingpong

//////////////////////////
harmso=(0,0,0,0,0,0,0,0);
spo=0;
;

noglow():=(
javascript("var ti=document.getElementById('osci0');var context = ti.getContext('2d');context.globalCompositeOperation = 'source-over'");
javascript("var ti=document.getElementById('osci1');var context = ti.getContext('2d');context.globalCompositeOperation = 'source-over'");
);
glow():=(
javascript("var ti=document.getElementById('osci0');var context = ti.getContext('2d');context.globalCompositeOperation = 'lighter'");
javascript("var ti=document.getElementById('osci1');var context = ti.getContext('2d');context.globalCompositeOperation = 'lighter'");
);


hitkeyw(n,mpos):=(
  regional(code,erg,pos);
  code=keycodes_(n+13);
  erg=false;

  if(code_1==0, //white key
     pos=(keyfrom+(code_3*7+(code_2-1))*keydiff,-5.5);;
     erg=(mpos.x>pos.x & mpos.x<pos.x+keydiff & mpos.y>pos.y & mpos.y<pos.y +3);
  );
  erg;
);


hitkeyb(n,mpos):=(
  regional(code,erg,pos);
  code=keycodes_(n+13);
  erg=false;
  if(code_1==1, //white key
     pos=(keyfrom+(code_3*7+(code_2-1)+keyshifts_(code_2))*keydiff,-4.5);;
     erg=(mpos.x>pos.x & mpos.x<pos.x+keydiff*.6 & mpos.y>pos.y & mpos.y<pos.y +2);
  );

  erg;

);


keyforposition(pos):=(
   black=-99;
   white=-99;
   forall(-12..24,if(hitkeyw(#,pos),white=#););
   forall(-12..24,if(hitkeyb(#,pos),black=#););
   println(white+" "+black+" "+pos);
   if(black!=-99,black,white);

);


hitkeyboard(pos):=(
   black=-99;
   white=-99;
   forall(-12..24,if(hitkeyw(#,pos),white=#););
   forall(-12..24,if(hitkeyb(#,pos),black=#););
   println(white+" "+black+" "+pos);
   if(black!=-99,
     callmidi(144,60+black,100),
     if(white!=-99,callmidi(144,60+white,100));

   );
);


releasekeyboard(pos):=(
   black=-99;
   white=-99;
   forall(-12..24,if(hitkeyw(#,pos),white=#););
   forall(-12..24,if(hitkeyb(#,pos),black=#););
   println(white+" "+black+" "+pos);
   if(black!=-99,
     callmidi(128,60+black,127),
     if(white!=-99,callmidi(128,60+white,127));

   );
);


</script>
<script id="csmultidown" type="text/x-cindyscript">
  dictnumb=multiid()+1;
  pos=mouse(id->multiid()).xy;
  key=keyforposition(pos);
  fingerstokeys_(dictnumb)=key;
  if(key!=-99,callmidi(144,60+key,100));


  if(|pos.y|<1,
      fingerstofreqs_dictnumb=pos.x;
      playfromline(invmov(pos.x),dictnumb);

  );

</script>

<script id="csmultiup" type="text/x-cindyscript">
  dictnumb=multiid()+1;
  pos=mouse(id->multiid()).xy;
  callmidi(128,60+fingerstokeys_(dictnumb),127);
  fingerstokeys_(dictnumb)=-99;
  releasekeyboard(pos);
  if(fingerstofreqs_dictnumb!=-99,
     stopfromline(dictnumb);
  );
  fingerstofreqs_dictnumb=-99;

</script>


<script id="csmultidrag" type="text/x-cindyscript">
  pos=mouse(id->multiid()).xy;
  key=keyforposition(pos);
  dictnumb=multiid()+1;

  if(fingerstokeys_dictnumb!=key,
     callmidi(128,60+fingerstokeys_dictnumb,127);
     fingerstokeys_dictnumb=key;
     if(key!=-99,
       callmidi(144,60+key,100);
     );

  );
  if(fingerstofreqs_dictnumb!=-99,
    fingerstofreqs_dictnumb=pos.x;
    playfromline(invmov(pos.x),dictnumb);
  );


</script>

<script id="csmousedown" type="text/x-cindyscript">
    moving=true;
    resampled=false;
  pos=mouse().xy;
//  hitkeyboard(pos);
</script>

<script id="csmouseup" type="text/x-cindyscript">
  pos=mouse().xy;
//  releasekeyboard(pos);
    moving=false;
    if(resampled,resample());

  hit=0;

currentbuttons=[];
if(timbreselected,currentbuttons=buttons_1);
if(tuningselected,currentbuttons=buttons_2);
if(analyseselected,currentbuttons=buttons_3);

hit=0;
  apply(1..length(currentbuttons),i,b=currentbuttons_i;
    box=b_3;

    if(mouse().x>box_1_1 & mouse().x<box_2_1 & mouse().y>box_1_2 & mouse().y<box_3_2 ,
       hit=i;
    );
  );
  if(timbreselected &hit!=0,

      if(contains(1..5,hit),
        apply(1..5,i,
            buttons_1_i_4=i==hit;
        );
      );
      if(contains(6..8,hit),
        apply(6..8,i,
            buttons_1_i_4=i==hit;
        );
      );

      if(hit==1,settimbre(timbre1);setphases(phases1););
      if(hit==2,settimbre(timbre2);setphases(phases2););
      if(hit==3,settimbre(timbre3);setphases(phases3););
      if(hit==4,settimbre(timbre4);setphases(phases4););
      if(hit==5,settimbre(timbre5);setphases(phases5););
      if(hit==6,setdampingpick());
      if(hit==7,setdampingdamp());
      if(hit==8,setdampinghold());
  );
  if(tuningselected &hit!=0,
      apply(1..length(buttons_2),i,
          buttons_2_i_4=(i==hit);
      );
      if(hit==1,settuning(tuningequal));
      if(hit==2,settuning(tuningpyth));
      if(hit==3,settuning(tuningjust));
      if(hit==4,settuning(indie7));
  );
  if(analyseselected &hit!=0,
      apply(1..length(buttons_3),i,
          buttons_3_i_4=(i==hit);
      );
     if(hit==1,setanalyser("dissonance"));
     if(hit==2,setanalyser("ratios"));
     if(hit==3,setanalyser("lissajous"));
     if(hit==4,setanalyser("circle"));
     if(hit==5,setanalyser("wave"));
  );







if(|mouse().xy,timbrebuttonpos+(mainiconextend,mainiconextend)/2|<mainiconextend/2,
   timbreselected=!timbreselected;
   if(timbreselected,analyseselected=false;tuningselected=false);
);
if(|mouse().xy,tuningbuttonpos+(mainiconextend,mainiconextend)/2|<mainiconextend/2,
   tuningselected=!tuningselected;
   if(tuningselected,analyseselected=false;timbreselected=false);
);

if(|mouse().xy,analysebuttonpos+(mainiconextend,mainiconextend)/2|<mainiconextend/2,
   analyseselected=!analyseselected;
   if(analyseselected,timbreselected=false;tuningselected=false);
);

if(|mouse().xy,(-15,-3)|<.5,ftype=0;resample());
if(|mouse().xy,(-15,-4)|<.5,ftype=1;resample());
if(|mouse().xy,(-15,-5)|<.5,ftype=2;resample());


</script>

<script id="csdraw" type="text/x-cindyscript">
drawtext((-15.9,-1),mdim+" "+mdia+" "+mdib,color->(1,1,1)*.3,size->20);
drawtext((-15.9,-1.5),hitkeys,color->(1,1,1)*.3,size->20);
//drawtext((-15,4),partials,size->30,color->(1,1,1));
//drawtext((-15,6),fingerstofreqs,size->30,color->(1,1,1));

drawtext((-15.5,-3),"log(F)",color->(1,1,1)*if(ftype==0,1,.3),size->20);
drawtext((-15.5,-4),"F",color->(1,1,1)*if(ftype==1,1,.3),size->20);
drawtext((-15.5,-5),"-1/F",color->(1,1,1)*if(ftype==2,1,.3),size->20);

FR.y=13;
FR.x=max(4,min(10,FR.x));

fr=2^((FR.x-7)/3)*261;
basefr=fr;
sp=(PR.x-PL.x)/7;;
ssca=(SR.x-SL.x)/7;;

PL.y=7.8;
PL.size=0;
PL.x=5;
PR.y=7.8;


SL.y=7;
SL.size=0;
SL.x=5;
SR.y=7;



apply(1..8,
  (top_(#)).x=PL.x+(#-1)*(PR.x-PL.x)/7;
  (base_(#)).x=PL.x+(#-1)*(PR.x-PL.x)/7;
  (top_(#)).y=11.5;
  (base_(#)).y=8.5;
);
h1=|C,T|;
h2=|E,U|;
h3=|G,V|;
h4=|K,W|;
h5=|M,X|;
h6=|O,Y|;
h7=|Q,Z|;
h8=|S,P0|;
harms=(h1,h2,h3,h4,h5,h6,h7,h8);
harms=harms/|harms|;
harm=harms;
if(analyser=="dissonance"&
((|harmso,harms|>0.0001)
%(sp!=spo)
%(fr!=fro))
,resample());
harmso=harms;
spo=sp;
fro=fr;




A.y=0;
f1=fr;
f2=f1*2^(A.x/12);
//....ANALYZER DRAWING


// DISSONACNE CURVE

if(analyser=="dissonance",
  connect(sample,color->(.5,1,.5),size->2);
);


// CIRCLE OF FIFTH

tonenames=("$C$","$C^\sharp$","$D$","$E^\flat$","$E$","$F$","$F^\sharp$","$G$","$A^\flat$","$A$","$B^\flat$","$H$");

if(analyser=="circle",
   tonset=set(apply(hitkeys,mod(#,12)));
   rad=3;
   center1=(-8,4.5);
   drawcircle(center1,rad,color->(1,1,1));
   center2=(1,4.5);
   drawcircle(center2,rad,color->(1,1,1));
   deltaw=1/12*360°;

   apply(0..11,
      w=#*deltaw;
      draw(center1+(sin(w),cos(w))*rad,
           center1+(sin(w+7*deltaw),cos(w+7*deltaw))*rad,
           color->(1,1,1)*.5);
      draw(center2+(sin(w),cos(w))*rad,
           center2+(sin(w+7*deltaw),cos(w+7*deltaw))*rad,
           color->(1,1,1)*.5);
   );
   htpolygon=apply(tonset,w=#*deltaw;center1+(sin(w),cos(w))*rad);
   fillpoly(htpolygon,color->(1,.8,.5),alpha->.7);
   drawpoly(htpolygon,color->(1,1,1),size->3);

   tonset2=sort(apply(tonset,mod(7*#,12)));
   quipolygon=apply(tonset2,w=#*deltaw;center2+(sin(w),cos(w))*rad);
   fillpoly(quipolygon,color->(1,.8,.5),alpha->.7);
   drawpoly(quipolygon,color->(1,1,1),size->3);



   apply(0..11,
      isin=contains(tonset,#);
      w=#*deltaw;
      fillcircle(center1+(sin(w),cos(w))*rad,.4,color->(.5,1,.5)*if(isin,.7,.3));
      fillcircle(center2+(sin(7*w),cos(7*w))*rad,.4,color->(.5,1,.5)*if(isin,.7,.3));
      drawtext(center1+(sin(w),cos(w)-.04)*rad,tonenames_(#+1),color->(1,1,1),align->"center",size->24);
      drawtext(center2+(sin(7*w),cos(7*w)-.04)*rad,tonenames_(#+1),color->(1,1,1),align->"center",size->24);
   );

);

// RATIOS

if(analyser=="ratios",
  freqs=sort(apply(hitkeys,tunenote(#)));
  tposs=sort(apply(hitkeys,mov(#)));
  reffreq=tunenote(12);
  if(length(tposs)>1,
    apply(tposs,
       draw((#,0),(#,2),color->(.5,1,.5),size->2);
     );
     apply(tposs,
       draw((#,2),color->(.5,1,.5),size->3);
     );
     draw((tposs_(1),2),(tposs_(-1),2),color->(.5,1,.5),size->2);
    );

  apply(1..(length(tposs)-1),
       erg=myguess((freqs_(#+1))/reffreq,(freqs_(#))/reffreq);
       ergpos=((tposs_(#+1))+(tposs_(#)))/2;
       drawtext((ergpos,2.4),erg,color->(1,1,1),size->40,align->"center");
     );
  if(length(tposs)>2,
     draw((tposs_(1),4),(tposs_(-1),4),color->(.5,1,.5)*.8,size->2);
     draw((tposs_(1),2),(tposs_(1),4),color->(.5,1,.5)*.8,size->2);
     draw((tposs_(-1),2),(tposs_(-1),4),color->(.5,1,.5)*.8,size->2);
     draw((tposs_(1),4),color->(.5,1,.5),size->3)*.8;
     draw((tposs_(-1),4),color->(.5,1,.5),size->3*.8);

       erg=myguess((freqs_(-1))/reffreq,(freqs_(1))/reffreq);
       ergpos=((tposs_(1))+(tposs_(-1)))/2;
       drawtext((ergpos,4.4),erg,color->(1,1,1),size->40,align->"center");
  );

  if(ftype==2,
     t0=movorig(1000);
     t1=movorig(-12);
     apply(tposs,t,
        plot(sin((x-t0)*2*pi/|t,t0|)*|t,t0|*.2,color->(1,.8,.5),stop->t0,start->t1);
        plot(sin((x-t0)*2*pi/|t,t0|)*|t,t0|*.2,color->(1,.8,.5),stop->t0,start->t,size->3);
        draw((t,0));
     );
  );
);

// FUNCTIONS
if(analyser=="wave2",
  freqs=sort(apply(hitkeys,tunenote(#)));


  if(freqs==[],
     fingfr=select(fingerstofreqs,#!=-99);
     freqs=sort(apply(fingfr,invmov(#)));
     freqs=apply(freqs,x,((2^(1/12))^x));
  );
  j=0;

  time=seconds()-timestamp;
  if(freqs!=[],
    mult=2;
    apply(freqs,fr,
      sh=(time)*f1/16*mult;
      sh=((floor(sh))*2*pi)/fr;
      sh=0;
      if(!beating,
        plot(
         sum(1..8,i,harm_i*sin(((i^sp)*(x+14-sh))*fr*mult+phases_i))*.7+5,
         start->-14,stop->14,color->(hue(j/8)*.5+(1,1,1)*.5));
         ,
               plot(
         sum(1..8,i,harm_i*sin(((i^sp)*(x+14-sh))*(fr+1*beats)*mult+phases_i))*.35+
         sum(1..8,i,harm_i*sin(((i^sp)*(x+14-sh))*(fr-1*beats)*mult+phases_i))*.35

         +5,
         start->-14,stop->14,color->(hue(j/8)*.5+(1,1,1)*.5));
         );
      j=j+1;
    );

    plot(
      3+
      sum(freqs,fr,
         sh=(time)*f1/16*mult;
         sh=((floor(sh))*2*pi)/fr;
         sum(1..8,i,harm_i*sin(((i^sp)*(x+14-sh))*fr*mult+phases_i))/length(freqs)
      ),
     start->-14,stop->14,color->(1,1,1),size->2);

  );
);


// FUNCTIONS
if(analyser=="wave",
  freqs=sort(apply(hitkeys,tunenote(#)));


  if(freqs==[],
     fingfr=select(fingerstofreqs,#!=-99);
     freqs=sort(apply(fingfr,invmov(#)));
     freqs=apply(freqs,x,((2^(1/12))^x));
  );
  j=0;

  time=seconds()-timestamp;
  if(freqs!=[],
    mult=2;
    sh=time;
    drawtext((-15,5),color->(1,1,1),freqs*basefr,size->20);
    drawtext((-15,5.5),color->(1,1,1),time,size->20);
    sh=floor(sh*freqs_1*basefr)/freqs_1/basefr;
    if(length(freqs)>1,
       oldsh=sort(apply((sh-20)..sh,shi,shdiff=|shi*freqs_2*basefr,oldsh*freqs_2*basefr|;|shdiff-round(shdiff)|))_1;
       sh=oldsh;
    );
    prevsh=sh;
    drawtext((-15,6),color->(1,1,1),"SHIFT: "+sh*freqs_1*basefr,size->20);

    plot(
      3+
      sum(freqs,fr,
        // sh=(time)*f1/16*mult;
        // sh=((floor(sh))*2*pi)/fr;
         ti=(((x+14)/261/28*10-sh)*fr*basefr);
         sum(1..8,i,harm_i*sin((2*pi*(i^sp)*ti+phases_i))/length(freqs)
      )),
     start->-14,stop->14,color->(1,1,1),size->2,pxlres->10);
   );

);








if(analyser!="lissajous",
 DA.visible=false;
 );
 if(analyser=="lissajous",
 DA.visible=true;
 drawpolygon(((-6,1),(4,1),(4,11),(-6,11)),color->[1,1,1]*.3);
 draw((5,1),(5,5),color->(1,1,1)*.6);
 DA.x=5;
 DA.y=min(5,max(DA.y,1));
 oszidamp=(DA.y-1)/4;
 freqs=sort(apply(hitkeys,tunenote(#)));

  if(freqs==[],
     fingfr=select(fingerstofreqs,#!=-99);
     freqs=sort(apply(fingfr,invmov(#)));
     freqs=apply(freqs,x,((2^(1/12))^x));
  );
  damping=(oszidamp!=0);
  dt=seconds()-timestamp;
//  dt=.01;
  timestamp=seconds();
  if(length(freqs)>=2,
    hmax=sum(harms);
    ffre(t):=sum(1..8,i,sin((i^sp)*t+phases_i)*harms_i)/((hmax)^(.7));
    aa=freqs_1*50;
    bb=freqs_2*50;

    N = 300;
    alphaf = .85;
    if(damping,alphaf = 1);
    pts = [[
        ffre(ta)*if(damping,exp(-tadamp*oszidamp*4),1),
        ffre(tb)*if(damping,exp(-tbdamp*oszidamp*4),1)
      ]*.95]++apply(1..N,
      ta=ta+dt*aa*2*pi/N;
      tb=tb+dt*bb*2*pi/N;
      tadamp=tadamp+dt*.02;
      tbdamp=tbdamp+dt*.02;
      la=ffre(ta)*if(damping,exp(-tadamp*oszidamp*4),1);
      lb=ffre(tb)*if(damping,exp(-tbdamp*oszidamp*4),1);

      pt=(la,lb);
      pt*.95;
    );
    otherosci = 1-currentosci;
    mult = re(.8^(1/N));

glow();

    clearimage("osci"+currentosci);
    canvas((-1.05,-1.05),(1.05,-1.05),"osci"+currentosci,
      drawimage((-1.05,-1.05),(1.05,-1.05),"osci"+otherosci,alpha->alphaf);
      apply(1..(length(pts)-1),
        draw(pts_#,pts_(#+1),color->(0.5,0.5,1),alpha->alphaf^((N-#)/N),size->.21);
      );
    );
    drawimage((-6,1),(4,1), "osci"+currentosci);

/*

 //   clearimage("osci0");
 glow();
    canvas((-1.05,-1.05),(1.05,-1.05),"osci0",
      apply(1..(length(pts)-1),
       // advanceto(pts_#,alphaf^((N-#)/N));
        draw(pts_#,pts_(#+1),color->(0.5,0.5,1),alpha->alphaf^((N-#)/N),size->.21);
      );
    );
    drawimage((-6,1),(4,1), "osci0");
*/
    currentosci = 1-currentosci;
 )

);



advanceto(pt,al):=(
if(oldpt!=(0,0),
  len=|pt,oldpt|;
  advleno=advlen;
  advlen=advlen+len;
  while(adv<advlen,
    lambda=(adv-advleno)/len;
    println(lambda);
     ptt=pt*(lambda)+oldpt*(1-lambda);
     fillcircle(ptt,.01,color->(0.5,0.5,1),alpha->.5);
     adv=adv+.01;
  );
  //   fillcircle(ptt,.008,color->(1,0.5,1),alpha->.5);
);
  oldpt=pt;
);


/////////////////////////

tones=apply(1..37,(#,(#-13)));

drawtext((mov(0),-1),"1",size->20,align->"center",color->(1,1,1));
drawtext((mov(12),-1),"2",size->20,align->"center",color->(1,1,1));
drawtext((mov(24),-1),"4",size->20,align->"center",color->(1,1,1));
drawtext((mov(-12),-1),"1/2",size->20,align->"center",color->(1,1,1));

draw((mov(tones_1_2),0),(mov(tones_37_2),0),color->(1,1,1));
//apply(-12..24,x=mov(#);  draw((x,-.2),(x,.2),color->(1,1,1)));
apply(tones,x=mov(#_2);  draw((x,-.2),(x,.2),color->(1,1,1)));
apply(-1..2,x=mov(tones_(#*12+13)_2);draw((x,-.2),(x,.2),size->3,color->(1,1,1)));
//apply(tones,draw((mov(#_2),0)));


draw(PL+(1.5,0),PL,arrow->true,color->(1,1,1));
draw(PR-(1.5,0),PR,arrow->true,color->(1,1,1));
drawtext((PL+PR)/2+(0,-.2),size->24,align->"center","spread="+sp,color->(1,1,1));
//drawtext(PL+(0,7),size->20,"timbre",color->(1,1,1));
//drawtext((4,14),size->20,"prime tone",color->(1,1,1));
//drawtext((4,12.2),size->16,round(fr)+"Hz",color->(1,1,1));


draw(SL+(1.5,0),SL,arrow->true,color->(1,1,1));
draw(SR-(1.5,0),SR,arrow->true,color->(1,1,1));
drawtext((SL+SR)/2+(0,-.2),size->24,align->"center","scale="+ssca,color->(1,1,1));
//drawtext(SL+(0,7),size->20,"timbre",color->(1,1,1));


keycodes=[
(0,1,0),
  (1,1,0),
(0,2,0),
  (1,2,0),
(0,3,0),
(0,4,0),
  (1,4,0),
(0,5,0),
  (1,5,0),
(0,6,0),
  (1,6,0),
(0,7,0),
(0,1,1),
  (1,1,1),
(0,2,1),
  (1,2,1),
(0,3,1),
(0,4,1),
  (1,4,1),
(0,5,1),
  (1,5,1),
(0,6,1),
  (1,6,1),
(0,7,1),
(0,1,2),
  (1,1,2),
(0,2,2),
  (1,2,2),
(0,3,2),
(0,4,2),
  (1,4,2),
(0,5,2),
  (1,5,2),
(0,6,2),
  (1,6,2),
(0,7,2),
(0,1,3)
];

keyfrom=-14;
keyto=14;
keydiff=(keyto-keyfrom)/22;
keydocks=zerovector(length(keycodes));
keydockpos=zerovector(length(keycodes));
keydockshifts=(.3,.5,.7,.3,.45,.55,.7);

drawkeyw(n,mode):=(
  regional(code,nn);
  code=keycodes_(n+13);

  if(code_1==0, //white key
     pos=(keyfrom+(code_3*7+(code_2-1))*keydiff,-5.5);;
     fillpoly(
        [pos,pos+(keydiff,0),pos+(keydiff,3),pos+(0,3)],
        color->if(contains(hitkeys,n),(1,1,1),(1,1,1)*.8);
     );
     drawpoly(
        [pos,pos+(keydiff,0),pos+(keydiff,3),pos+(0,3)],
        color->(0,0,0),size->3
     );
     keydockpos_(n+13)=pos+(keydiff*keydockshifts_(code_2),3);
  //   drawtext(pos+(.1,.1),code);
     keydockpos_(n+13)=(-13.6+(n+12)*keydiff*.585,3+pos.y);

  );

);
keyshifts=(.6,.8,0,.6,.7,.8);
drawkeyb(n,mode):=(
  regional(code,nn);
  code=keycodes_(n+13);

  if(code_1==1, //white key
     pos=(keyfrom+(code_3*7+(code_2-1)+keyshifts_(code_2))*keydiff,-4.5);;
     fillpoly(
        [pos,pos+(keydiff*.6,0),pos+(keydiff*.6,2),pos+(0,2)],
        color->if(contains(hitkeys,n),(1,1,1)*.4,(0,0,0));
     );
     drawpoly(
        [pos,pos+(keydiff*.6,0),pos+(keydiff*.6,2),pos+(0,2)],
        color->(0,0,0),size->3
     );
   //  keydockpos_(n+13)=pos+(keydiff*.3,2);
     keydockpos_(n+13)=(-13.6+(n+12)*keydiff*.585,2+pos.y);

    // drawtext(pos+(.1,.1),code);

  );

);

forall(-12..24,drawkeyw(#,0););
forall(-12..24,drawkeyb(#,0););
//drawall(keydockpos);
apply(tones,draw((mov(#_2),0),keydockpos_(#_1),color->(.5,.5,1),size->if(contains(hitkeys,(#_1)-13),4,1)));


draw((4,13),(10,13),color->(1,1,1),size->2,alpha->0.2);


/*
apply(buttons,b,
  box=b_3;
  selected=b_4;
  fillpoly(box,color->if(selected,(1,1,1)*.3,(1,1,1)*.4));
  drawpoly(box,color->(1,1,1),size->if(selected,3,1));
  drawtext(box_1+(.2,.3),b_2,size->20,color->(1,1,1));
);

*/

//....TOP LEVEL UI
drawimage(timbrebuttonpos,timbrebuttonpos+(mainiconextend,0),"timbreIcon",alpha->if(timbreselected,1,.5));
drawimage(tuningbuttonpos,tuningbuttonpos+(mainiconextend,0),"tuningIcon",alpha->if(tuningselected,1,.5));
drawimage(analysebuttonpos,analysebuttonpos+(mainiconextend,0),"analyseIcon",alpha->if(analyseselected,1,.5));
drawtext(timbrebuttonpos+(mainiconextend/2,.4),"Timbre",color->(1,.8,.8),align->"center",size->25,alpha->if(timbreselected,1,.7));
drawtext(tuningbuttonpos+(mainiconextend/2,.4),"Stimmung",color->(.8,.8,1),align->"center",size->25,alpha->if(tuningselected,1,.7));
drawtext(analysebuttonpos+(mainiconextend/2,.4),"Analyse",color->(.8,1,.8),align->"center",size->25,alpha->if(analyseselected,1,.7));

if(!timbreselected,
  Beats.visible=false;
);

if(timbreselected,
Beats.visible=true;
Beats.y=(timbrebox_1_2+1);
bmin=timbrebox_1_1+4.5;
bmax=timbrebox_1_1+7;
Beats.x=min(bmax,max(bmin,Beats.x));
beats=(Beats.x-bmin)/(bmax-bmin);
beating=(beats!=0);

  draw(timbrebuttonpos+(mainiconextend/2,0),timbrebuttonpos+(mainiconextend/2,-.5),color->(1,1,1),size->2);
  fillpoly(timbrebox,color->(1,.5,.5)*.3,alpha->.9);
  drawpoly(timbrebox,color->(1,1,1),size->2);
  draw((bmin,Beats.y),(bmax,Beats.y),color->(1,1,1)*.7);

);


if(tuningselected,
  draw(tuningbuttonpos+(mainiconextend/2,0),tuningbuttonpos+(mainiconextend/2,-.5),color->(1,1,1),size->2);
  fillpoly(tuningbox,color->(.5,.5,1)*.3,alpha->.9);
  drawpoly(tuningbox,color->(1,1,1),size->2);
);


if(analyseselected,
  draw(analysebuttonpos+(mainiconextend/2,0),analysebuttonpos+(mainiconextend/2,-.5),color->(1,1,1),size->2);
  fillpoly(analyserbox,color->(.5,1,.5)*.3,alpha->.9);
  drawpoly(analyserbox,color->(1,1,1),size->2);
);

currentbuttons=[];
if(timbreselected,currentbuttons=buttons_1);
if(tuningselected,currentbuttons=buttons_2);
if(analyseselected,currentbuttons=buttons_3);

apply(currentbuttons,b,
  box=b_3;
  selected=b_4;

  text=b_2;
  tok=tokenize(text,"_");
  if(length(tok)==2,
    drawimage(box_1,box_2,tok_1,alpha->if(selected,1,.6));
    drawtext(box_1+(0,.4)+(1.25,0),tok_2,align->"center",size->18,color->(1,1,1),alpha->if(selected,1,.6));
  ,

    fillpoly(box,color->if(selected,(1,1,1)*.3,(1,1,1)*.4));
    drawpoly(box,color->(1,1,1),size->if(selected,3,1));
    drawtext(box_1+(.2,.3),b_2,size->20,color->(1,1,1));
  );
);

if(ftype==2,draw((movorig(1000),0),color->(1,1,1)*.5));
if(ftype==1,draw((movorig(-1000),0),color->(1,1,1)*.5));

apply(1..20,
   if(fingerstofreqs_#!=-99,
       fillcircle((fingerstofreqs_#,0),.7,color ->hue(#/10),alpha->.5);
   );
);


</script>
<script id="cskeydown" type="text/x-cindyscript">
  println("KEYDOWN");

apply(1..19,
   if(key()==keys_#,
     newkey=59+#;
     if(!contains(hitkeys,#-1),
         callmidi(144,59+#,100));
     )
);

</script>

<script id="cskeyup" type="text/x-cindyscript">
  println("KEYUP");
apply(1..19,
   if(key()==keys_#,callmidi(128,59+#,127));
);

</script>
    <script type="text/javascript">
var cdy = CindyJS({
  scripts: "cs*",
  defaultAppearance: {
    dimDependent: 0.7,
    fontFamily: "sans-serif",
    lineSize: 1,
    pointSize: 5.0,
    textsize: 12.0
  },
  angleUnit: "°",
  geometry: [
        {name: "FR", type: "Free", pos: [7,7], color: [1.0, 1.0, 1.0],narrow:80,alpha:0.2},

      {name: "PL", type: "Free", pos: [8,7], color: [1.0, 1.0, 1.0],narrow:80},
    {name: "PR", type: "Free", pos: [15,7], color: [1.0, 1.0, 1.0],narrow:80},
        {name: "SR", type: "Free", pos: [7,6], color: [1.0, 1.0, 1.0],narrow:80},

      {name: "SL", type: "Free", pos: [8,6], color: [1.0, 1.0, 1.0],narrow:80},
      {name: "DA", type: "Free", pos: [8,1], color: [1.0, 1.0, 1.0],narrow:80},
      {name: "Beats", type: "Free", pos: [-15,1], color: [1.0, 1.0, 1.0],narrow:80},


  //  {name: "A", type: "Free", pos: [4.0, 0.0, 0.849123854153823], color: [1.0, 1.0, 1.0],narrow:80},
    {name: "B", type: "Free", pos: [-3.2, -4.0, -0.4], color: [1.0, 0.0, 0.0], visible: false},
    {name: "C", type: "Free", pos: [4.0, 3.5, 0.5], color: [1.0, 0.0, 0.0], visible: false},
    {name: "a", type: "Segment", color: [1,1,1], args: ["B", "C"]},
    {name: "D", type: "Free", pos: [-3.6000000000000005, -4.0, -0.4], color: [1.0, 0.0, 0.0], visible: false},
    {name: "E", type: "Free", pos: [4.0, 3.111111111111111, 0.4444444444444444], color: [1.0, 0.0, 0.0], visible: false},
    {name: "b", type: "Segment", color: [1,1,1], args: ["D", "E"]},
    {name: "F", type: "Free", pos: [4.0, 4.0, 0.4], color: [1.0, 0.0, 0.0], visible: false},
    {name: "G", type: "Free", pos: [4.0, 2.8, 0.4], color: [1.0, 0.0, 0.0], visible: false},
    {name: "c", type: "Segment", color: [1,1,1], args: ["F", "G"]},
    {name: "H", type: "Free", pos: [4.0, 3.6363636363636362, 0.36363636363636365], color: [1.0, 0.0, 0.0], visible: false},
    {name: "K", type: "Free", pos: [4.0, 2.5454545454545454, 0.36363636363636365], color: [1.0, 0.0, 0.0], visible: false},
    {name: "d", type: "Segment", color: [1,1,1], args: ["H", "K"]},
    {name: "L", type: "Free", pos: [4.0, 3.3333333333333326, 0.33333333333333337], color: [1.0, 0.0, 0.0], visible: false},
    {name: "M", type: "Free", pos: [4.0, 2.3333333333333335, 0.33333333333333337], color: [1.0, 0.0, 0.0], visible: false},
    {name: "e", type: "Segment", color: [1,1,1], args: ["L", "M"]},
    {name: "N", type: "Free", pos: [4.0, 3.076923076923077, 0.3076923076923077], color: [1.0, 0.0, 0.0], visible: false},
    {name: "O", type: "Free", pos: [4.0, 2.1538461538461537, 0.3076923076923077], color: [1.0, 0.0, 0.0], visible: false},
    {name: "f", type: "Segment", color: [1,1,1], args: ["N", "O"]},
    {name: "P", type: "Free", pos: [4.0, 2.8571428571428577, 0.2857142857142857], color: [1.0, 0.0, 0.0], visible: false},
    {name: "Q", type: "Free", pos: [4.0, 2.0, 0.2857142857142857], color: [1.0, 0.0, 0.0], visible: false},
    {name: "g", type: "Segment", color: [1,1,1], args: ["P", "Q"]},
    {name: "R", type: "Free", pos: [4.0, 2.6666666666666656, 0.26666666666666666], color: [1.0, 0.0, 0.0], visible: false},
    {name: "S", type: "Free", pos: [4.0, 1.8666666666666667, 0.26666666666666666], color: [1.0, 0.0, 0.0], visible: false},
    {name: "h", type: "Segment", color: [1,1,1], args: ["R", "S"]},
    {name: "T", type: "PointOnSegment", pos: [-3.2, -4.0, -0.4], color: [1,.5,.5], args: ["a"],size:3,narrow:80},
    {name: "U", type: "PointOnSegment", pos: [-3.6000000000000005, -4.0, -0.4], color: [1,.5,.5], args: ["b"],size:3,narrow:80},
    {name: "V", type: "PointOnSegment", pos: [-3.9999999999999996, -4.0, -0.4], color: [1,.5,.5], args: ["c"],size:3,narrow:80},
    {name: "W", type: "PointOnSegment", pos: [4.0, 3.6363636363636354, 0.3636363636363637], color: [1,.5,.5], args: ["d"],size:3,narrow:80},
    {name: "X", type: "PointOnSegment", pos: [4.0, 2.9999999999999987, 0.3333333333333333], color: [1,.5,.5], args: ["e"],size:3,narrow:80},
    {name: "Y", type: "PointOnSegment", pos: [4.0, 2.2381334352944777, 0.3076923076923076], color: [1,.5,.5], args: ["f"],size:3,narrow:80},
    {name: "Z", type: "PointOnSegment", pos: [4.0, 2.0782667613448718, 0.28571428571428575], color: [1,.5,.5], args: ["g"],size:3,narrow:80},
    {name: "P0", type: "PointOnSegment", pos: [4.0, 1.8666666666666665, 0.26666666666666666], color: [1,.5,.5], args: ["h"],size:3, printname: "$P_{0}$",narrow:80}
  ],
  images:{
    timbreIcon:"icons/TimbreIcon.png",
    tuningIcon:"icons/TuningIcon.png",
    analyseIcon:"icons/AnalyseIcon.png",
    ratiosIcon:"icons/ratiosIcon.png",
    dissonanceIcon:"icons/dissonanceIcon.png",
    lissajousIcon:"icons/lissajousIcon.png",
    fifthIcon:"icons/FifthIcon.png",
    waveformIcon:"icons/WaveFormIcon.png",
    sineTimbreIcon:"icons/Sine.png",
    sawTimbreIcon:"icons/Saw.png",
    squareTimbreIcon:"icons/Square.png",
    triangleTimbreIcon:"icons/Triangle.png",
    pulseTimbreIcon:"icons/Pulse.png",
    dampTimbreIcon:"icons/DampIcon.png",
    holdTimbreIcon:"icons/HoldIcon.png",
    pickTimbreIcon:"icons/PickIcon.png"

  },
  ports: [{
    id: "CSCanvas",
    width: 1920,
    height: 1440,
    transform: [{visibleRect: [-16,12,16,-6]}],
    grid: 1.0,
    background: "rgb(30,30,30)"
  }],
  csconsole: false,
  cinderella: {build: 1901, version: [2, 9, 1901]}
});
    </script>

        <script>

    //Bare minimum JS code to read midi input
    //Adapted from https://github.com/cwilso/WebMIDIAPIShim

    var midi;
    var log = document.getElementById("midi-log");
    init();

    function init() {
      logText("Initializing MIDI...");
      navigator.requestMIDIAccess().then( onSuccess, onFailure ); //get midi access
    }

    function onSuccess( access ) {

      midi = access;
      var inputs = midi.inputs;

      logText("Found " + inputs.size + " MIDI input(s)");

      //connect to first device found
      if(inputs.size > 0) {
        var iterator = inputs.values(); // returns an iterator that loops over all inputs
     //   console.log(inputs);
        for(i=0;i<inputs.size;i++) {
        var input = iterator.next().value; // get the first input
      //    logText("Connected  input: " +i+" ==> "+ input.name);
          input.onmidimessage = handleMIDIMessage;
        }
      }
    }

    function onFailure( err ) {
      logText("MIDI Init Error. Error code: " + err.code);
    }

    function handleMIDIMessage(event){

      //event.data & event.receivedTime are populated
      //event.data has 3 components:
      //0) The device id
      //1) The controller id
      //2) The controller value (typically in the range 0 - 127)

      if (event.data.length === 3) {
        console.log(event.data);
        cdy.evokeCS("callmidi("+event.data[0]+","+event.data[1]+","+event.data[2]+")");
      //  logText('controller id: ' + event.data[1] +  ', value: ' + event.data[2]);
      }
    }

    function logText(str){
      /*
      log.innerHTML += str;
      log.innerHTML += "<br>";
      log.scrollTop = log.scrollHeight;
      */
    }

  </script>
</head>
<body>
    <div id="CSCanvas"></div>
</body>
</html>
